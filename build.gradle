plugins {
    id 'java'
    id 'maven-publish'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group = "tr.com.infumia"
version = "1.0-SNAPSHOT"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    jcenter()
    spigot()
    jitpack()
    codemc()
    maven {
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
    maven {
        url = "http://dl.bintray.com/tastybento/maven-repo"
    }
    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    // Tools
    implementation("co.aikar:acf-bukkit:0.5.0-SNAPSHOT")
    implementation("co.aikar:idb-bukkit:1.0.0-SNAPSHOT")
    // Other Stuffs
    compileOnly("tr.com.infumia:KekoUtil:7.0")
    compileOnly("org.jetbrains:annotations:20.1.0")
    compileOnly("org.projectlombok:lombok:1.18.12")
    annotationProcessor("org.projectlombok:lombok:1.18.12")
    // Main
    compileOnly spigot()
}

shadowJar {
    // TODO Change the package name as you want.
    relocate('co.aikar.commands', this.group + ".kekotemplate.commands")
    relocate('co.aikar.idb', this.group + ".kekotemplate.db")
    setArchivesBaseName(this.name)
    archiveClassifier.set("")
    archiveVersion.set("")
}

task gradleVersion(type: Wrapper) {
    gradleVersion = '6.6'
}

spigot {
    // TODO Change the description as you want.
    description = "Bukkit plugin template to create plugins as fast as possible."
    apiVersion = "1.13"
    // TODO Change the authors as you want.
    authors = ["infumia"]
    softDepends = ["KekoTemplate"]
    // TODO Change the commands/permissions as you want.
    commands {
        kekotemplate {
            description = 'Main command of the plugin.'
            permission = 'kekotemplate.command.main'
            permissionMessage = 'You do not have permission!'
            usage = '/<command> [help|reload|version]'
            aliases = ['bt']
        }
    }
    permissions {
        'kekotemplate.*' {
            description = 'Allows you to access to the all plugin.'
            defaults = 'op'
            children = [
                'kekotemplate.command.*': true
            ]
        }
        'kekotemplate.command.*' {
            description = 'It allows you to use to all commands of the plugin.'
            defaults = 'op'
            children = [
                'kekotemplate.command.help'   : true,
                'kekotemplate.command.reload' : true,
                'kekotemplate.command.version': true,
                'kekotemplate.command.message': true
            ]
        }
        'kekotemplate.version' {
            description = 'It allows you to notify if there is a new update for the plugin on join.'
            defaults = 'op'
        }
        'kekotemplate.command.main' {
            description = 'It allows you to use `/kekotemplate` command.'
            defaults = 'true'
        }
        'kekotemplate.command.help' {
            description = 'It allows you to use to `/kekotemplate help` command.'
            defaults = 'true'
        }
        'kekotemplate.command.reload' {
            description = 'It allows you to use to `/kekotemplate reload` command.'
            defaults = 'op'
        }
        'kekotemplate.command.version' {
            description = 'It allows you to use to `/kekotemplate version` command.'
            defaults = 'op'
        }
        'kekotemplate.command.message' {
            description = 'It allows you to use to `/kekotemplate message` command.'
            defaults = 'true'
        }
    }
}
